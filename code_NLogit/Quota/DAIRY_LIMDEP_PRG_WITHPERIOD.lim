? ******************************************************************
? *       Patrick R. Gillespie                            
? *       Walsh Fellow                    
? *       Teagasc, REDP                           
? *       patrick.gillespie@teagasc.ie            
? ******************************************************************
? *
? *       Dairy Efficiency Models
? *         for the purpose of generating 
? *         Malmquist indices of TFP
? * 
? *       Code for PhD Thesis chapter
? *         based on original specification and code by 
? * 
? *          James Carroll, TCD (2008, Thesis)
? *      
? *                                                    
? *       Thesis Supervisors:
? *       -------------------
? *       Cathal O'Donoghue
? *       Stephen Hynes
? *       Thia Hennessey
? *       Fiona Thorne
? *
? ******************************************************************
? *  Contributions:
? *    - Additional years added (not in this code)
? *    - Code mostly rewritten to automate addition of years
? *    - Addition of specification testing and diagnostics
? *    - Reworking of spreadsheets generating TFP (more automation)
? *    - Generation of NFS weights (separate spreadsheet and code) 
? ******************************************************************










? ******************************************************************
? ------------------------ DATA IMPORT -----------------------------
? ******************************************************************

? NOTE ON IMPORTING DATA -------------------------------------------
?
? If the file throws an error on import: 
?  1  : Open and save the file again (without changes, including  
?         filename) I'm not sure why this is necessary, but it seems 
?         to work. I suspect it has something to do with 
?         [compatibility mode] in recent versions of Excel. 
?         I'm using Excel 2010. 
?
?  2  : If the file is .xlsx, save it as .xls
?
?  3  : Check and see if the file has blank cells. If so, try using 
?         the missing() option on export excel in Stata.
?
?  I'm using "export excel" in Stata to write the Excel file, and 
?    I've had to do "1" from above every time to read the file in. 
?
?  NLogit/Limdep is supposed to be able to read csv files as well, 
?    but I've never gotten it to work properly; often it LOOKS 
?    like it's correctly in memory, but it reads only half the file!
? ------------------------------------------------------------------


RESET$ 
READ;FILE="D:\Data\data_NFSPanelAnalysis\code_NLogit\Quota\DAIRY.xls"
    ;FORMAT=xls$



? Check that all the years came in, and see how many cases there are 
?   before any more sample selection takes place (already only 
?   specialist dairy)
DSTAT;Rhs=YEAR$

? ******************************************************************
? -------------------- END OF DATA IMPORT --------------------------
? ******************************************************************










? ******************************************************************
? -------------------- PROCEDURE DEFINITIONS -----------------------
? ******************************************************************

?-------------------------
? Sample Selection 
?-------------------------

? Drop zero's, negatives, and weights less than 1

PROC = ZERODROP $

  REJECT  ;W < 1$
  ?????REJECT	;Y1<=0$
  REJECT	;Y2<=0$
  REJECT	;H<=0$
  REJECT	;C<=0$
  REJECT	;L3<=0$
  REJECT	;D<=0$
  REJECT	;A<=0$
END PROCEDURE



? Select groups of years via T index

PROC = SAMPSEL(min_t, max_t) $

  SAMPLE;ALL$
  REJECT  ;T < min_t $
  REJECT  ;T > max_t $

  EXEC; ZERODROP$

ENDPROCEDURE



? -----------------------
? Yearly summary stats
? ----------------------- 

? Get summary stats per year, puts  

PROC = YSTATS(namelist, tindex) $

  SAMPLE  ; ALL $ 
  EXEC; ZERODROP$
  REJECT  ;T < tindex $
  REJECT  ;T > tindex $


  DSTAT	;RHS=namelist;WTS=W $
  MATRIX; YSTATMAT = [YSTATMAT / LASTDSTA] $

END PROCEDURE






? ---------------------------
?  Saving parameter estimates 
?  and elasticities
? ---------------------------

PROC = MKRTS(FIRSTB)$

  CREATE	
	;B1  = b(FIRSTB)
	;B2  = b(FIRSTB +  1)
	;B3  = b(FIRSTB +  2)
	;B4  = b(FIRSTB +  3)
	;B5  = b(FIRSTB +  4)
	;B11 = b(FIRSTB +  5)
	;B12 = b(FIRSTB +  6)
	;B13 = b(FIRSTB +  7)
	;B14 = b(FIRSTB +  8)
	;B15 = b(FIRSTB +  9)
	;B22 = b(FIRSTB + 10)
	;B23 = b(FIRSTB + 11)
	;B24 = b(FIRSTB + 12)
	;B25 = b(FIRSTB + 13)
	;B33 = b(FIRSTB + 14)
	;B34 = b(FIRSTB + 15)
	;B35 = b(FIRSTB + 16)
	;B44 = b(FIRSTB + 17)
	;B45 = b(FIRSTB + 18)
	;B55 = b(FIRSTB + 19)$

  DSTAT	;RHS =  B1 , B2 , B3 , B4 , B5 , B11, B12, B13, B14, B15,
	        B22, B23, B24, B25, B33, B34, B35, B44, B45, B55$

  ?Using parameter estimates to calc. elasticities 
  CREATE ;EH=B1+(2*LNH*B11)+(LND*B12)+(LNC*B13)+(LNL*B14)+(LNA*B15)$
  CREATE ;ED=B2+(LNH*B12)+(2*LND*B22)+(LNC*B23)+(LNL*B24)+(LNA*B25)$
  CREATE ;EC=B3+(LNH*B13)+(LND*B23)+(2*LNC*B33)+(LNL*B34)+(LNA*B35)$
  CREATE ;EL=B4+(LNH*B14)+(LND*B24)+(LNC*B34)+(2*LNL*B44)+(LNA*B45)$
  CREATE ;EA=B5+(LNH*B15)+(LND*B25)+(LNC*B35)+(LNL*B45)+(2*LNA*B55)$

  ? Using the elasticities to calculate Returns to Scale
  CREATE	;RTS=EH+ED+EC+EL+EA$


  ? Summarise elasticities and RTS overall, and year by year
  DSTAT	;RHS=EH,ED,EC,EL,EA,RTS;WTS=W$


ENDPROCEDURE





?-------------------------
? RANKS (BY YEAR) 
?-------------------------

PROC = MKRNKS $

  EXEC; PROC = SAMPSEL(i,i) $
  CREATE	;RKPL2=RNK(UPL)$
  CREATE	;RKBC2=RNK(UBC)$
  CREATE	;RKTRE2=RNK(UTRE)$
  CREATE	;RKTFE2=RNK(UTFE)$

ENDPROCEDURE





? ******************************************************************
? -------------- END OF PROCEDURE DEFINITIONS ----------------------
? ******************************************************************










? ******************************************************************
? --------------------- DATA MANIPULATION --------------------------
? ******************************************************************

?-------------------------------------------------------------------
? Get boundary values of T for panel. Also get second year for 
?  situations where we initialise a procedure in the first year, then
? iterate 
?  over the rest.
?-------------------------------------------------------------------
CALC; MINT = MIN(T) $
CALC; MINT2 = MINT + 1 $
CALC; MAXT = MAX(T) $


?---------------------
? Rename Direct Costs
?---------------------
RENAME	;DC=D$


?------------ 
? CREATE LAND
?------------
CREATE	;A=LANDFAGE$


?------------ 
? CREATE Panel Dummies
?------------
CREATE	;IF(              YEAR <= 1983) PANEL1=1$
CREATE	;IF(YEAR > 1983 & YEAR <= 1991) PANEL2=1$
CREATE	;IF(YEAR > 1991               ) PANEL3=1$


?--------------
? Summary stats
?--------------
DSTAT	;RHS=Y2,H,C,D,L3,A,YEAR$


?------------------
? Sample selection
?------------------
EXEC; PROC = SAMPSEL(MINT,MAXT) $


?----------------------- 
? CREATE PANEL INDICATOR
?----------------------- 
SORT	;LHS=FC;RHS=*$
CREATE	;IND=NDX(FC,0)$
REGRESS	;LHS=ONE;RHS=ONE;STR=FC;PANEL$
CREATE	;GRPSIZE=_GROUPTI$
CALC	;LIST;G=MAX(GRPSIZE)$


?----------------
? CALCULATE MEANS
?----------------
CALC	;MEANY1=XBR(Y1)$
CALC	;MEANY2=XBR(Y2)$
CALC	;MEANH=XBR(H)$
CALC	;MEANC=XBR(C)$
CALC	;MEAND=XBR(D)$
CALC	;MEANL=XBR(L3)$
CALC	;MEANA=XBR(A)$


?--------------------------------------------------------------------
? MEAN ADJUSTMENT -- Eases interpretation of parameter estimates
?                      Makes it so B's are elasticities as in 
?                      Cobb-Douglas, even for the Translog functional
?                      form. 
?--------------------------------------------------------------------
CREATE	;MY1=Y1/MEANY1 
 	;MY2=Y2/MEANY2 
	;MH=H/MEANH 
	;MC=C/MEANC 
	;MD=D/MEAND  
	;ML=L3/MEANL 
                ;MA=A/MEANA$  


?--------------------------------------------------------------------
? CONVERT TO LOGS -- Both CD and TL functions are non-linear, 
?                      but this transforms them s.t. they are now 
?                      linear in parameters, hence they no longer 
?                      violate assumptions of OLS (which are the same
?                      that we must adhere to for these SFA models).
?--------------------------------------------------------------------
?????;LNY1=LOG(MY1) (neg values to fix... not using for now)
CREATE;LNY2=LOG(MY2)
	;LNH=LOG(MH)
	;LND=LOG(MD)
	;LNC=LOG(MC)
	;LNL=LOG(ML) 
	;LNA=LOG(MA)$


?-------------------------------------------
? CREATE INTERACTION VARIABLES FOR TRANSLOG
?-------------------------------------------
 CREATE ;TT=T*T
        ;LNHH=LNH*LNH
        ;LNHD=LNH*LND
        ;LNHC=LNH*LNC
        ;LNHL=LNH*LNL
        ;LNDD=LND*LND
        ;LNDC=LND*LNC
        ;LNDL=LND*LNL
        ;LNCC=LNC*LNC
        ;LNCL=LNC*LNL
        ;LNLL=LNL*LNL
        ;LNHA=LNH*LNA
        ;LNDA=LND*LNA
        ;LNCA=LNC*LNA
        ;LNLA=LNL*LNA
        ;LNAA=LNA*LNA
        ;LNHT=LNH*T
        ;LNDT=LND*T
        ;LNCT=LNC*T
        ;LNLT=LNL*T
        ;LNAT=LNA*T
        ;TTT=TT*T$


?------------------------------------------------------------
? CREATE NAMELISTS -- Similar to macros for Stata, allows you
?                      to give lists of variables. You cannot
?                      store other info this way however (e.g.
?                      conditions states, filepaths, etc.)
?------------------------------------------------------------
 NAMELIST;CD=ONE,LNH,LND,LNC,LNL,LNA$

 NAMELIST;TL=LNHH,LNHD,LNHC,LNHL,LNHA,LNDD,LNDC,LNDL,LNDA,
            LNCC,LNCL,LNCA,LNLL,LNLA,LNAA$

 NAMELIST;TCD=T,TT$

 NAMELIST;TTL=T,TT,LNHT,LNDT,LNCT,LNLT,LNAT$

 NAMELIST;TDV=  T2, T3, T4, T5, T6,
                T7, T8, T9,T10,T11,
               T12,T13,T14,T15,T16,
               T17,T18,T19,T20,T21,
               T22,T23,T24,T25,T26,
               T27,T28,T29,T30,T31, 
               T32,T33,T34 $

DSTAT	;RHS=TDV $


? ******************************************************************
? --------------------- DATA MANIPULATION --------------------------
? ******************************************************************










? ******************************************************************
? ---------------------- MODEL ESTIMATION --------------------------
? ******************************************************************


? Define list of RHS vars for all subsequent models.
? Includes one PANEL dummy (any more is collinear). Used > 1984 as 
?  base case, so 1 if before then.

NAMELIST; X = CD,TL,TDV,PANEL1 $

? NOTE: before each model, make sure you have the correct sample.
?       LIMDEP/NLOGIT allows you to return previously rejected obs
?       back into the sample. To avoid accidentally expanding the 
?       sample, I created the SAMPSEL procedure at the top of this
?       file to just run all the REJECT commands again. Also takes
?       a start and end T, so you can specify an individual year or 
?       a range of years. I do this by creating and passing in 
?       MINT and MAXT which guarantees that each model is using the 
?       same years, and MAXT is also used for creating the elasticity
?       matrices (yearly DSTATS of the elasticity variables). 



?===================================================================
? PIT AND LEE 
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

FRONTIER
	;LHS=LNY2
        ;RHS=X
	;PDS=GRPSIZE
	;EFF=UPL$

CREATE;EPL=EXP(-UPL)$
MATRIX;B_PL = B$



?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(2) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T,EPL,EH,ED,EC,EL,EA,CD $



? Do the DSTATS for the first year and initialise matrix X
EXEC  ; PROC = SAMPSEL(i,i); i=MINT $
DSTAT	; RHS  = elist ; WTS=W$
MATRIX; YSTATMAT = LASTDSTA $


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i=MINT2,MAXT $


? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_PL = YSTATMAT $
MATRIX; DELETE YSTATMAT $




?===================================================================




?===================================================================
? B AND C 1992 
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

FRONTIER
	;LHS=LNY2
	;RHS=X
	;PDS=GRPSIZE
	;MODEL=BC
	;EFF=UBC$

CREATE;EBC=EXP(-UBC)$
MATRIX;B_BC = B$


?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(2) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T,EBC,EH,ED,EC,EL,EA,CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC; PROC = SAMPSEL(MINT,MINT) $
DSTAT	;RHS=elist;WTS=W$
MATRIX; YSTATMAT = LASTDSTA$


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i=MINT2,MAXT $

? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_BC = YSTATMAT $
MATRIX; DELETE YSTATMAT $


?===================================================================






?===================================================================
? 'True' Random Effects Model WITH TIME DUMMIES 
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

? Requires initial run for starting parameter values.
? Surround with SILENT and NOSILENT to suppress output.

SILENT$
  FRONTIER
	;LHS=LNY2
	;RHS=X$
NOSILENT$


? Actual model.
FRONTIER
	;LHS=LNY2
	;RHS=X
	;PDS=GRPSIZE
	;HALTON
	;PTS=10
	;RPM
	;FCN=ONE(n)
	;EFF=UTRE$

CREATE	;ETRE=EXP(-UTRE)$
MATRIX;B_TRE = B$

?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(1) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T, ETRE,EH,ED,EC,EL,EA,CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC; PROC = SAMPSEL(MINT,MINT) $
DSTAT	;RHS=elist;WTS=W$
MATRIX; YSTATMAT = LASTDSTA$


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i=MINT2,MAXT $


? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_TRE = YSTATMAT $
MATRIX; DELETE YSTATMAT $


?===================================================================







?===================================================================
? 'True' Fixed Effects Model TIME DUMMIES
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

? Requires initial run for starting parameter values.
? Surround with SILENT and NOSILENT to suppress output.
SILENT$
  FRONTIER
	;LHS=LNY2
	;RHS=X$
NOSILENT$


? Actual model.
FRONTIER
	;LHS=LNY2
	;RHS=X
	;PDS=GRPSIZE
	;FEM
	;EFF=UTFE$

CREATE;ETFE=EXP(-UTFE)$
MATRIX;B_TFE = B$

?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(1) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T,ETFE,EH,ED,EC,EL,EA,CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC; PROC = SAMPSEL(MINT,MINT) $
DSTAT	;RHS=elist;WTS=W$
MATRIX; YSTATMAT = LASTDSTA$


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i=MINT2,MAXT $


? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_TFE = YSTATMAT $
MATRIX; DELETE YSTATMAT $


?===================================================================

? ******************************************************************
? --------------------- END OF ESTIMATION --------------------------
? ******************************************************************










? ******************************************************************
? --------------------- MODEL COMPARISONS --------------------------
? ******************************************************************


?----------------------------------------------------------
? CORRELATION MATRIX OF TE SCORES (ACROSS ALL YEARS)
?----------------------------------------------------------
DSTAT;Rhs=UPL,UBC,UTRE,UTFE;Wts=W;Output=2$


?----------------------------------------------------------
? CREATE RANKS AND COMPARE CORRELATIONS (ACROSS ALL YEARS)
?----------------------------------------------------------
CREATE	;RKPL=RNK(UPL)$
CREATE	;RKBC=RNK(UBC)$
CREATE	;RKTRE=RNK(UTRE)$
CREATE	;RKTFE=RNK(UTFE)$
DSTAT;Rhs=RKPL,RKBC,RKTRE,RKTFE;Wts=W;Output=2$


?----------------------------------------------------------
? CREATE RANKS AND COMPARE CORRELATIONS (BY YEAR)
?----------------------------------------------------------
EXEC    ;PROC = MKRNKS ;i=1,MAXT   ; SILENT $
EXEC    ;PROC = SAMPSEL(MINT, MAXT); SILENT $
DSTAT   ;Rhs=RKPL2,RKBC2,RKTRE2,RKTFE2;Wts=W;Output=2$


?----------------------------------------------------------
? PLOT (YEARLY) INEFFICIENCY RANKINGS 
?----------------------------------------------------------
PLOT    ;Lhs=RKPL2 ;Rhs=RKBC2$
PLOT    ;Lhs=RKTFE2;Rhs=RKTRE2$
PLOT    ;Lhs=RKTRE2;Rhs=RKPL2$



?----------------------------------------------------------
? Export farm-year specific inefficiency terms and rankings to .xls
?----------------------------------------------------------
WRITE
  ;T,W,FC,
   UPL,UBC,UTRE,UTFE,
   RKPL,RKPL2,RKBC,RKBC2,
   RKTRE,RKTRE2,RKTFE,RKTFE2
  ;FILE="D:\Data\data_NFSPanelAnalysis\Do_Files\Quota\UITS_DY.xls"
  ;Format=xls$

? ******************************************************************
? --------------------- END OF COMPARISONS -------------------------
? ******************************************************************










? ******************************************************************
? ----------- EFFICIENCY EFFECTS MODEL ESTIMATION ------------------
? ******************************************************************

? Make logged quota var and list of environmental (z) variables.
CREATE; LNQT = LOG(QT + 0.01)$
CREATE; LNALLOC = LOG(ALLOC + 0.00001)$

NAMELIST; Z = ONE, SOIL2, DECOUP, EXTEND, LNALLOC, LNQT$


?===================================================================
? B AND C 1995 
?===================================================================

FRONTIER	;LHS = LNY2;RHS=CD,TL,TDV
		;PDS = GRPSIZE
		;MODEL = BC 
            ;EFF = UBCZ
            ;HET
		;HFU = Z $



PLOT; LHS = UBC; RHS = UBCZ$



CREATE; EBCZ  = EXP(-UBCZ) $
MATRIX; B_BCZ = B $


?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(2) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T, EBCZ, EH, ED, EC, EL, EA, CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC   ; PROC = SAMPSEL(MINT, MINT) $
DSTAT  ; RHS = elist ; WTS = W $
MATRIX ; YSTATMAT = LASTDSTA $


? Use procedure for remaining years
EXEC   ; PROC = YSTATS(elist,i) ; i = MINT2, MAXT $

? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX ; EM_BCZ = YSTATMAT $
MATRIX ; DELETE YSTATMAT $


?===================================================================





?===================================================================
? 'True' Random Effects Model WITH TIME DUMMIES 
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

? Requires initial run for starting parameter values.
? Surround with SILENT and NOSILENT to suppress output.

SILENT$
  FRONTIER
	;LHS = LNY2
	;RHS = X$
NOSILENT$


? Actual model.
FRONTIER
	;LHS = LNY2
	;RHS = X
	;PDS = GRPSIZE
	;HALTON
	;PTS = 10
	;RPM
	;FCN = ONE(n)
	;EFF = UTREZ
      ;HET
	;HFU = Z $

CREATE	;ETREZ = EXP(-UTREZ)$
MATRIX;B_TREZ = B$

?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(1) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T, ETREZ,EH,ED,EC,EL,EA,CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC; PROC = SAMPSEL(MINT,MINT) $
DSTAT	;RHS = elist;WTS = W$
MATRIX; YSTATMAT = LASTDSTA$


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i = MINT2,MAXT $


? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_TREZ = YSTATMAT $
MATRIX; DELETE YSTATMAT $


?===================================================================





?===================================================================
? 'True' Fixed Effects Model TIME DUMMIES
?===================================================================
EXEC; PROC = SAMPSEL(MINT,MAXT) $

? Requires initial run for starting parameter values.
? Surround with SILENT and NOSILENT to suppress output.
SILENT$
  FRONTIER
	;LHS = LNY2
	;RHS = X$
NOSILENT$


? Actual model.
FRONTIER
	;LHS = LNY2
	;RHS = X
	;PDS = GRPSIZE
	;FEM
	;EFF = UTFEZ
      ;HET
      ;HFU = Z $


      ;HFV = Z $



CREATE;ETFEZ = EXP(-UTFEZ)$
MATRIX;B_TFEZ = B$

?----------------------------------------------------------
? Use procedure to make elasticities and returns to scale
?----------------------------------------------------------
EXEC  ;PROC = MKRTS(1) $


?----------------------------------------------------------
? Get the average elasticities for each year of the sample
?----------------------------------------------------------

? First create a list of the varnames (includes time index, 
?   technical efficiency estimate, and logged primary vars)
NAMELIST  ; elist = T,ETFEZ,EH,ED,EC,EL,EA,CD $


? Do the DSTATS for the first year and initialise matrix X
EXEC; PROC = SAMPSEL(MINT,MINT) $
DSTAT	;RHS = elist;WTS = W$
MATRIX; YSTATMAT = LASTDSTA$


? Use procedure for remaining years
EXEC ;PROC = YSTATS(elist,i) ;i = MINT2,MAXT $


? Copy EM for this model.Delete YSTATMAT in preparation for next model.
MATRIX; EM_TFEZ = YSTATMAT $
MATRIX; DELETE YSTATMAT $

?===================================================================



? ******************************************************************
? ------------- END OF EFFICIENCY EFFECTS MODELS -------------------
? ******************************************************************










? ******************************************************************
? -------------------------- END OF FILE ---------------------------
? ******************************************************************
